#pragma kernel CS_InitializeSpectrum
#pragma kernel CS_PackSpectrumConjugate
#pragma kernel CS_UpdateSpectrumForFFT
#pragma kernel CS_HorizontalFFT
#pragma kernel CS_VerticalFFT
#pragma kernel CS_AssembleMaps

#define PI 3.14159265358979323846

RWTexture2DArray<float4> _SpectrumTextures, _InitialSpectrumTextures, _DisplacementTextures;
RWTexture2DArray<float2> _SlopeTextures;

float _FrameTime, _Gravity, _RepeatTime, _Depth, _LowCutoff, _HighCutoff;
int _Seed;
float2 _Lambda, _NormalStrength;
uint _N, _LengthScale0, _LengthScale1, _LengthScale2, _LengthScale3;
float _FoamBias, _FoamDecayRate, _FoamAdd, _FoamThreshold;

/**
 * @brief Komplex számok szorzása
 * @param a Első komplex szám (valós, képzetes) formában 
 * @param b Második komplex szám (valós, képzetes) formában 
 * @return A komplex szorzás eredménye (valós, képzetes) formában
*/
float2 ComplexMult(float2 a, float2 b){
    return float2(a.x * b.x - a.y * b.y, a.x * b.y + a.y * b.x);
}

/**
 * @brief Szög komplex számmá alakítása polár formában (Euler-képlet segítségével)
 * @param x Szög radiánban
 * @return Komplex szám (cos(x), sin(x)) formában
*/
float2 EulerFormula(float x){
    return float2(cos(x), sin(x));
}

/**
 * @brief Integer hash függvény Hugo Elias algoritmusa alapján
 * @param n Előjel nélküli egész szám kezdőérték
 * @return Pszeudo-véletlenszerű lebegőpontos szám
*/
float hash(uint n){
    n = (n << 13U) ^ n;
    n = n * (n * n * 15731U + 0x789221U) + 0x1376312589U;
    return float(n & uint(0x7fffffffU)) / float(0x7fffffff);
}

/**
 * @brief Egyenletes eloszlású véletlen minták Gauss-eloszlásúvá alakítása a Box-Muller transzformáció segítségével
 * @param u1 Első egyenletes eloszlású véletlen minta
 * @param u2 Második egyenletes eloszlású véletlen minta
 * @return Két független Gauss-eloszlású véletlen változó
*/
float2 UniformToGaussian(float u1, float u2){
    float R = sqrt(-2.0 * log(u1));
    float theta = 2.0 * PI * u2;
    return float2(R * cos(theta), R * sin(theta));
}

// Forrás: https://github.com/gasgiant/FFT-Ocean/
/**
 * @brief Óceanográfiai spektrum tulajdonságok
 */
struct SpectrumParameters{
    float scale;
    float angle;
    float spreadBlend;
    float swell;
    float alpha;
    float peakFrequency;
    float gamma;
    float shortWavesFade;
};

StructuredBuffer<SpectrumParameters> _Spectrums;

// Forrás: https://m.cdn.blog.hu/da/darthasylum/tutorials/C++/ch56_ocean.html
/**
 * @brief Vízhullámok diszperziós relációja
 * @param kMag Hullámvektor nagysága (hullámszám)
 * @return Hullámok szögfrekvenciája a hullámszám alapján
*/
float Dispersion(float kMag){
    return sqrt(_Gravity * kMag * tanh(min(kMag * _Depth, 20)));
}

/**
 * @brief Calculates derivative of dispersion relation with respect to wavenumber
 * @param kMag Magnitude of wave vector (wavenumber)
 * @return Derivative of dispersion relation
 */
float DispersionDerivative(float kMag){
    float th = tanh(min(kMag * _Depth, 20));
    float ch = cosh(kMag * _Depth);
    return _Gravity * (_Depth * kMag / ch / ch + th) / Dispersion(kMag) / 2.0;
}

/**
 * @brief Diszperziós reláció deriváltja a hullámszám szerint
 * @param kMag Hullámvektor nagysága (hullámszám)
 * @return A diszperziós reláció deriváltja
*/
float NormalizationFactor(float s){
    float s2 = s * s;
    float s3 = s2 * s;
    float s4 = s3 * s;
    if (s < 5){
        return -0.000564 * s4 + 0.00776 * s3 - 0.044 * s2 + 0.192 * s + 0.163;
    }else{
        return -4.80e-08 * s4 + 1.07e-05 * s3 - 9.53e-04 * s2 + 5.90e-02 * s + 3.93e-01;
    }
}

/**
 * @brief Irányított terjedési függvény
 * @param theta Irányszög radiánban
 * @param s Terjedési paraméter, amely az irányítottság koncentrációját szabályozza
 * @return Irányított terjedési érték
*/
float Cosine2s(float theta, float s){
    return NormalizationFactor(s) * pow(abs(cos(0.5 * theta)), 2.0 * s);
}

/**
 * @brief Terjedési hatvány a frekvenciaarány alapján
 * @param frequency Aktuális hullámfrekvencia
 * @param peakFrequency A spektrum csúcsfrekvenciája
 * @return Terjedési hatvány paraméter
*/
float SpreadPower(float frequency, float peakFrequency){
    if (frequency > peakFrequency){
        return 9.77 * pow(abs(frequency / peakFrequency), -2.5);
    }else{
        return 6.97 * pow(abs(frequency / peakFrequency), 5.0);
    }
}

/**
 * @brief Irányított terjedési függvény a hullámspektrumhoz
 * @param theta Irányszög radiánban
 * @param frequency Aktuális hullámfrekvencia
 * @param spectrum Spektrum paraméterek struktúrája
 * @return Irányított terjedési tényező
*/
float DirectionSpectrum(float theta, float frequency, SpectrumParameters spectrum){
    float s = SpreadPower(frequency, spectrum.peakFrequency) + 
              16 * tanh(min(frequency / spectrum.peakFrequency, 20)) * 
              spectrum.swell * spectrum.swell;
    return lerp(2.0 / 3.1415 * cos(theta) * cos(theta), 
                Cosine2s(theta - spectrum.angle, s), 
                spectrum.spreadBlend);
}

/**
 * @brief TMA (Texel-Marsen-Arsloe) korrekció véges vízmélységhez
 * @param frequency Hullámfrekvencia
 * @param depth Vízmélység
 * @return Korrekciós tényező sekélyvízi hatásokhoz
*/
float TMACorrection(float frequency){
    float wH = frequency * sqrt(_Depth / _Gravity);
    if (wH <= 1.0){
        return 0.5 * wH * wH;
    }
    if (wH < 2.0){
        return 1.0 - 0.5 * (2.0 - wH) * (2.0 - wH);
    }
    return 1.0;
}

/**
 * @brief Exponenciális csillapítás hullámhosszokra
 * @param kMag Hullámvektor nagysága (hullámszám)
 * @param spectrum Spektrum paraméterek struktúrája
 * @return Csillapítási tényező rövid hullámokhoz
*/
float ShortWavesFade(float kMag, SpectrumParameters spectrum){
    return exp(-spectrum.shortWavesFade * spectrum.shortWavesFade * kMag * kMag);
}

/**
 * @brief JONSWAP-TMA hullámspektrum modell
 * @param frequency Hullámfrekvencia
 * @param spectrum Spektrum paraméterek struktúrája
 * @return Spektrális energiasűrűség az adott frekvencián
*/
float JONSWAP_TMA(float frequency, SpectrumParameters spectrum){
    float sigma = (frequency <= spectrum.peakFrequency) ? 0.07 : 0.09;

    //https://wikiwaves.org/Ocean-Wave_Spectra#JONSWAP_Spectrum
    float r = exp(-pow(frequency - spectrum.peakFrequency, 2) / 
                  (2.0 * pow(sigma, 2) * pow(spectrum.peakFrequency, 2)));

    //https://apps.dtic.mil/sti/pdfs/ADA157975.pdf
    //float r = exp(-pow(frequency / (1 - spectrum.peakFrequency), 2) / 
    //                    (2.0 * pow(sigma, 2), 2));
    
    float frqRcp = rcp(frequency);
    float peakFoverF = spectrum.peakFrequency / frequency;

    return spectrum.scale * 
           spectrum.alpha * (_Gravity * _Gravity) *
           frqRcp * frqRcp * frqRcp * frqRcp * frqRcp * 
           TMACorrection(frequency) *
           exp(-1.25 * peakFoverF * peakFoverF * peakFoverF * peakFoverF) *
           pow(abs(spectrum.gamma), r);
}

/**
 * @brief Inicializálja a hullámspektrumot a JONSWAP-TMA modell és az irányított terjedés használatával
 * @param id Szál azonosító
 * 
 * Kezdeti adatok létrehozása a spektrumok alapján.
 * Gauss-eloszlású véletlen változók generalása realisztikus hullámminták létrehozásához megfelelő statisztikai tulajdonságokkal.
 * Spektrummódosítások a hullámfrekvencia, irány és mélység alapján.
*/
[numthreads(32, 32, 1)]
void CS_InitializeSpectrum(uint3 id : SV_DISPATCHTHREADID){
    uint seed = id.x + _N * id.y + _N;
    seed += _Seed;

    float lengthScales[4] = { _LengthScale0, _LengthScale1, _LengthScale2, _LengthScale3 };

    [unroll]
    for (uint i = 0; i < 4; ++i){
        float halfN = _N / 2.0;
        float deltaK = 2.0 * PI / lengthScales[i];
        float2 K = (id.xy - halfN) * deltaK;
        float kMag = length(K);

        seed += i + hash(seed) * 10;
        float4 uniformRandSamples = float4(hash(seed), hash(seed * 2), hash(seed * 3), hash(seed * 4));
        float2 gauss1 = UniformToGaussian(uniformRandSamples.x, uniformRandSamples.y);
        float2 gauss2 = UniformToGaussian(uniformRandSamples.z, uniformRandSamples.w);

        if (_LowCutoff <= kMag && kMag <= _HighCutoff){
            float kAngle = atan2(K.y, K.x);
            float frequency = Dispersion(kMag);
            float dFrequencydk = DispersionDerivative(kMag);

            float spectrum = JONSWAP_TMA(frequency, _Spectrums[i * 2]) * 
                             DirectionSpectrum(kAngle, frequency, _Spectrums[i * 2]) * 
                             ShortWavesFade(kMag, _Spectrums[i * 2]);
            
            if (_Spectrums[i * 2 + 1].scale > 0){
                spectrum += JONSWAP_TMA(frequency, _Spectrums[i * 2 + 1]) * 
                            DirectionSpectrum(kAngle, frequency, _Spectrums[i * 2 + 1]) * 
                            ShortWavesFade(kMag, _Spectrums[i * 2 + 1]);
            }
            
            _InitialSpectrumTextures[uint3(id.xy, i)] = float4(
                float2(gauss2.x, gauss1.y) * 
                sqrt(2 * spectrum * abs(dFrequencydk) / kMag * deltaK * deltaK), 
                0.0, 0.0);
        }else{
            _InitialSpectrumTextures[uint3(id.xy, i)] = 0.0;
        }
    }
}

/**
 * @brief Spektrum adatok rendezése az FFT számára konjugált párok létrehozásával
 * @param id Szál azonosító
*/
[numthreads(32, 32, 1)]
void CS_PackSpectrumConjugate(uint3 id : SV_DISPATCHTHREADID){
    [unroll]
    for (uint i = 0; i < 4; ++i){
        float2 h0 = _InitialSpectrumTextures[uint3(id.xy, i)].rg;
        float2 h0conj = _InitialSpectrumTextures[uint3((_N - id.x) % _N, (_N - id.y) % _N, i)].rg;

        _InitialSpectrumTextures[uint3(id.xy, i)] = float4(h0, h0conj.x, -h0conj.y);
    }
}

/**
 * @brief Hullámspektrum frissitése az idő múlásával
 * @param id Szál azonosító
 * 
 * Frissíti a hullámspektrumot az idő múlásával és a diszperziós relációk alkalmazásával.
 * Kiszámítja a hullámok elmozdulásait különböző irányokban.
 * Előkészíti az adatokat az FFT transzformációhoz.
*/
[numthreads(32, 32, 1)]
void CS_UpdateSpectrumForFFT(uint3 id : SV_DISPATCHTHREADID){
    float lengthScales[4] = { _LengthScale0, _LengthScale1, _LengthScale2, _LengthScale3 };

    [unroll]
    for (int i = 0; i < 4; ++i){
        float4 initialSignal = _InitialSpectrumTextures[uint3(id.xy, i)];
        float2 h0 = initialSignal.xy;
        float2 h0conj = initialSignal.zw;

        float halfN = _N / 2.0;
        float deltaK = 2.0 * PI / lengthScales[i];

        float2 K = (id.xy - halfN) * deltaK;
        float kMag = length(K);
        float kMagRcp = rcp(kMag);

        if (kMag < 0.0001){
            kMagRcp = 1.0;
        }


        float w_0 = 2.0 * PI / _RepeatTime;

        float dispersion = floor(sqrt(_Gravity * kMag) / w_0) * w_0 * _FrameTime;
        float2 exponent = EulerFormula(dispersion);


        float2 htilde = ComplexMult(h0, exponent) + ComplexMult(h0conj, float2(exponent.x, -exponent.y));
       

        float2 ih = float2(-htilde.y, htilde.x);
        float2 displacementX = ih * K.x * kMagRcp;
        float2 displacementY = htilde;
        float2 displacementZ = ih * K.y * kMagRcp;

        float2 displacementX_dx = -htilde * K.x * K.x * kMagRcp;
        float2 displacementY_dx = ih * K.x;
        float2 displacementZ_dx = -htilde * K.x * K.y * kMagRcp;

        float2 displacementY_dz = ih * K.y;
        float2 displacementZ_dz = -htilde * K.y * K.y * kMagRcp;

        float2 htildeDisplacementX = float2(displacementX.x - displacementZ.y, displacementX.y + displacementZ.x);
        float2 htildeDisplacementZ = float2(displacementY.x - displacementZ_dx.y, displacementY.y + displacementZ_dx.x);
        
        float2 htildeSlopeX = float2(displacementY_dx.x - displacementY_dz.y, displacementY_dx.y + displacementY_dz.x);
        float2 htildeSlopeZ = float2(displacementX_dx.x - displacementZ_dz.y, displacementX_dx.y + displacementZ_dz.x);

        _SpectrumTextures[uint3(id.xy, i * 2)] = float4(htildeDisplacementX, htildeDisplacementZ);
        _SpectrumTextures[uint3(id.xy, i * 2 + 1)] = float4(htildeSlopeX, htildeSlopeZ);
    }
}

#define SIZE 1024
#define LOG_SIZE 10

RWTexture2DArray<float4> _FourierTarget;
// D3D11 maximum méret 32kb
groupshared float4 fftGroupBuffer[2][SIZE];

/**
 * @brief Pillangó indexek és forgató tényezőket kiszámolása az FFT-hez
 * @param step Aktuális FFT lépés
 * @param index Szál index
 * @param indices Kimeneti paraméter a pillangó indexekhez
 * @param twiddle Kimeneti paraméter a forgató tényezőhöz
*/
void ButterflyValues(uint step, uint index, out uint2 indices, out float2 twiddle){
    const float twoPi = 6.28318530718;

    uint b = SIZE >> (step + 1); // Méret az aktuális lépéshez (felére csökken az FFT minden lépésében)

    // Eltolás az aktuális lépésben, lehetővé téve minden szál számára a megfelelő értékek elérését
    uint w = b * (index / b); // Alap index ehhez a pillangóhoz
    uint i = (w + index) % SIZE; // Aktuális elem ebben a szakaszban
    
    // komplex exponenciális érték, elforgatja az elemeket az FFT-ben
    sincos(-twoPi / SIZE * w, twiddle.y, twiddle.x); // Koszinusz és szinusz értékek a forgatáshoz
    // Ez teszi inverz FFT-vé
    twiddle.y = -twiddle.y;
    indices = uint2(i, i + b); // Indexek párja a pillangó kombinációhoz
    
}

/**
 * @brief Gyors Fourier-transzformáció
 * @param threadIndex Az aktuális szál indexe
 * @param input Transzformálandó bemeneti adatok
 * @return FFT-transzformált adatok
*/
float4 FFT(uint threadIndex, float4 input){
    fftGroupBuffer[0][threadIndex] = input;
    GroupMemoryBarrierWithGroupSync();
    bool pingpong = false;

    [unroll]
    for (uint step = 0; step < LOG_SIZE; ++step){
        uint2 inputsIndices;
        float2 twiddle;
        ButterflyValues(step, threadIndex, inputsIndices, twiddle);

        // Elforgatja a második indexnél lévő elemet a forgató tényezővel a ComplexMult használatával
        // majd kombinálja az első indexnél lévő elemmel
        float4 v = fftGroupBuffer[pingpong][inputsIndices.y];
        fftGroupBuffer[!pingpong][threadIndex] = fftGroupBuffer[pingpong][inputsIndices.x] + 
            float4(ComplexMult(twiddle, v.xy), ComplexMult(twiddle, v.zw));

        pingpong = !pingpong;
        GroupMemoryBarrierWithGroupSync();
    }

    return fftGroupBuffer[pingpong][threadIndex];
}

/**
 * @brief Vízszintes FFT a hullámspektrum adatokon
 * @param id Szál azonosító
 *
 * Az FFT algoritmust alkalmazza a hullámspektrum vízszintes dimenziója mentén.
 * Frekvenciatartománybeli adatokat alakít át térbeli tartományba.
*/
[numthreads(SIZE, 1, 1)]
void CS_HorizontalFFT(uint3 id : SV_DISPATCHTHREADID){
    for (int i = 0; i < 8; ++i){
        _FourierTarget[uint3(id.xy, i)] = FFT(id.x, _FourierTarget[uint3(id.xy, i)]);
    }
}

/**
 * @brief Függőleges FFT a hullámspektrum adatokon
 * @param id Szál azonosító
 *
 * Az FFT algoritmust alkalmazza a hullámspektrum függőleges dimenziója mentén.
 * Frekvenciatartománybeli adatokat alakít át térbeli tartományba.
*/
[numthreads(SIZE, 1, 1)]
void CS_VerticalFFT(uint3 id : SV_DISPATCHTHREADID){
    for (int i = 0; i < 8; ++i){
        _FourierTarget[uint3(id.yx, i)] = FFT(id.x, _FourierTarget[uint3(id.yx, i)]);
    }
}

/**
 * @brief Váltakozó előjelminta az FFT adatokra
 * @param data Bemeneti FFT adatok
 * @param id Szál azonosító
 * @return Permutált adatok váltakozó előjelekkel
*/
float4 Permute(float4 data, float3 id){
    return data * (1.0 - 2.0 * ((id.x + id.y) % 2));
}

/**
 * @brief A végső elmozdulás és lejtés térképek összeállítása az FFT eredményekből
 * @param id Szál azonosító
 * 
 * Kiszámítja a végső hullám elmozdulásokat, lejtéseket és habot a feldolgozott hullámspektrum alapján.
 * Kiszámítja a Jacobi-determinánst a felületi görbület és meredekség meghatározásához.
 * Habgenerálást alkalmaz a hullámtörési feltételek alapján.
 * Az eredményeket kimeneti textúrákban tárolja a rendereléshez.
*/
[numthreads(32, 32, 1)]
void CS_AssembleMaps(uint3 id : SV_DISPATCHTHREADID){
    [unroll]
    for (int i = 0; i < 4; ++i){
        float4 htildeDisplacement = Permute(_SpectrumTextures[uint3(id.xy, i * 2)], id);
        float4 htildeSlope = Permute(_SpectrumTextures[uint3(id.xy, i * 2 + 1)], id);

        float2 htildeDisplacementX = htildeDisplacement.rg;
        float2 htildeDisplacementZ = htildeDisplacement.ba;
        float2 htildeSlopeX = htildeSlope.rg;
        float2 htildeSlopeZ = htildeSlope.ba;
        
        float jacobian = (1.0f + _Lambda.x * htildeSlopeZ.x) * (1.0f + _Lambda.y * htildeSlopeZ.y) - 
                         _Lambda.x * _Lambda.y * htildeDisplacementZ.y * htildeDisplacementZ.y;

        float3 displacement = float3(_Lambda.x * htildeDisplacementX.x, 
                                    htildeDisplacementZ.x, 
                                    _Lambda.y * htildeDisplacementX.y);

        float2 slopes = htildeSlopeX.xy / (1 + abs(htildeSlopeZ * _Lambda));

        float foam = _DisplacementTextures[uint3(id.xy, i)].a;
        foam *= exp(-_FoamDecayRate);
        foam = saturate(foam);

        float biasedJacobian = max(0.0, -(jacobian - _FoamBias));

        if (biasedJacobian > _FoamThreshold){
            foam += _FoamAdd * biasedJacobian;
        }

        _DisplacementTextures[uint3(id.xy, i)] = float4(displacement, foam);
        _SlopeTextures[uint3(id.xy, i)] = float2(slopes);
    }
}
